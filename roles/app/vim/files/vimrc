"### Base ###
scriptencoding utf-8
filetype detect
syntax on

set number
set ruler
set nocompatible
set clipboard+=autoselect
set clipboard+=unnamed

" tmp files
set backup
set backupdir=~/.vim-tmp

set undofile
set undodir=~/.vim-tmp

set swapfile
set directory=~/.vim-tmp

" search
set incsearch
set hlsearch
set showmatch

" white characters
set list
set listchars=eol:$,tab:>\ ,extends:<

" indent
set expandtab
set tabstop=2
set shiftwidth=2
set softtabstop=2
set autoindent
set smartindent

" Disable cusor key.
nnoremap <Up> <NOP>
nnoremap <Down> <NOP>
nnoremap <Left> <NOP>
nnoremap <Right> <NOP>

inoremap <Up> <NOP>
inoremap <Down> <NOP>
inoremap <Left> <NOP>
inoremap <Right> <NOP>

" Keep indent of blank line.
nnoremap o oX<C-h>
nnoremap O OX<C-h>
inoremap <CR> <CR>X<C-h>

" Transition a command mode without entering a shift key.
nnoremap ; :

" Tab move
nnoremap <C-l> gt
nnoremap <C-h> gT



"### NeoBundle ###
if has('vim_starting')
  set runtimepath+=~/.vim/bundle/neobundle.vim
endif

call neobundle#begin(expand('~/.vim/bundle/'))

" Interactive command execution
NeoBundle 'Shougo/vimproc.vim', {
      \ 'build': {
      \   'windows': 'make -f make_mingw32.mak',
      \   'cygwin': 'make -f make_cygwin.mak',
      \   'mac': 'make -f make_mac.mak',
      \   'unix': 'make -f make_unix.mak',
      \ },
      \}

" Colors
NeoBundle 'altercation/vim-colors-solarized'

" Input completion
NeoBundle 'Shougo/neocomplete.vim'

" Shell
NeoBundle 'Shougo/vimshell.vim'

" Filer & Launcher
NeoBundle 'Shougo/unite.vim'

" Show outline
NeoBundle 'Shougo/unite-outline'

" Output the QuickFix content to the Unite buffer.
NeoBundle 'osyo-manga/unite-quickfix'

" Virtual Caps Lock
NeoBundle 'tpope/vim-capslock'

" Comment out
NeoBundle 'tyru/caw.vim'

" Show indent line
NeoBundle 'Yggdroot/indentLine'

" Highlight QuickFix
NeoBundle "jceb/vim-hier"

" YAML syntax/indent
NeoBundle 'mrk21/yaml-vim'

" CoffeeScript Syntax
NeoBundle 'kchmck/vim-coffee-script'

" TypeScript Syntax
NeoBundle 'leafgarland/typescript-vim'

" Stylus Syntax
NeoBundle 'wavded/vim-stylus'

" C++ Syntax
NeoBundleLazy 'vim-jp/cpp-vim'

" C++ Input completion (use Clang)
NeoBundleLazy 'Rip-Rip/clang_complete', {
      \ 'autoload': {'filetypes': 'cpp'}
      \}

" nginx syntax
NeoBundle 'evanmiller/nginx-vim-syntax'

" Wandbox
NeoBundle 'rhysd/wandbox-vim'

" Vimperator Syntax
NeoBundle 'BohrShaw/vim-vimperator-syntax'

" Haskell Syntax
NeoBundleLazy 'dag/vim2hs', {
      \ 'autoload': {'filetypes': 'haskell'}
      \}

" Haskell Static analisis (use ghc-mod)
NeoBundleLazy 'eagletmt/ghcmod-vim', {
      \ 'autoload': {'filetypes': 'haskell'}
      \}

" Haskell Input completion
NeoBundleLazy 'eagletmt/neco-ghc', {
      \ 'autoload': {'filetypes': 'haskell'}
      \}

" Slim
NeoBundle 'slim-template/vim-slim'

" Elixir
NeoBundle 'elixir-lang/vim-elixir'

" Markdown Syntax
NeoBundle 'plasticboy/vim-markdown'

" Bowser preview
NeoBundle 'kannokanno/previm', {
      \ 'depends': 'tyru/open-browser.vim'
      \}

call neobundle#end()

filetype plugin indent on
NeoBundleCheck



"### vim-colors-solarized ###
set background=dark
colorscheme solarized



"### NeoComplete.vim ###
let g:neocomplete#enable_at_startup = 1
let g:neocomplete#enable_ignore_case = 1
let g:neocomplete#enable_smart_case = 1

if !exists('g:neocomplete#keyword_patterns')
  let g:neocomplete#keyword_patterns = {}
endif

let g:neocomplete#keyword_patterns._ = '\h\w*'

inoremap <expr><TAB> pumvisible() ? "\<C-n>" : "\<TAB>"
inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<S-TAB>"

" for clang_complete
if !exists('g:neocomplete#force_omni_input_patterns')
  let g:neocomplete#force_omni_input_patterns = {}
endif

let g:neocomplete#force_overwrite_completefunc = 1
let g:neocomplete#force_omni_input_patterns.c =
      \ '[^.[:digit:] *\t]\%(\.\|->\)\w*'
let g:neocomplete#force_omni_input_patterns.cpp =
      \ '[^.[:digit:] *\t]\%(\.\|->\)\w*\|\h\w*::\w*'
let g:neocomplete#force_omni_input_patterns.objc =
      \ '[^.[:digit:] *\t]\%(\.\|->\)\w*'
let g:neocomplete#force_omni_input_patterns.objcpp =
      \ '[^.[:digit:] *\t]\%(\.\|->\)\w*\|\h\w*::\w*'



"### vimshell.vim ###
nnoremap <silent> ,s :<C-u>VimShell<CR>



"### Capslock.vim ###
imap <C-a> <C-O><Plug>CapsLockToggle



"### unite.vim ###
let g:unite_abbr_highlight = 'Normal'

" filer
function! g:UniteFiler()
  if getcwd() == expand('~')
    Unite file
  else
    call s:unite_gitignore_source()
    Unite file_rec
    call feedkeys('i')
  endif
endfunction

nnoremap <silent> f :<C-u>call g:UniteFiler()<CR>

" grep
let g:unite_source_grep_default_opts = '-iRHn'

nnoremap ,g :<C-u>Unite -buffer-name=search-buffer grep:::
vnoremap <silent> ,g y:Unite -buffer-name=search-buffer grep:::<C-R>=escape(@", ':\\.*$^[]')<CR><CR>
nnoremap <silent> ,r  :<C-u>UniteResume search-buffer<CR>

" other keymap
nnoremap t :<C-u>Unite tab<CR>
nnoremap b :<C-u>Unite buffer<CR>



" custum action: multiple file open
" see: http://d.hatena.ne.jp/thinca/20101031/1288531457
let s:unite_action = {
\   'is_selectable': 1,
\ }

function! s:unite_action.func(candidates)  " {{{
  tabnew `=a:candidates[0].action__path`
  for c in a:candidates[1 :]
    vsplit `=c.action__path`
  endfor
endfunction  " }}}

call unite#custom_action('openable', 'tabvsplit', s:unite_action)
unlet! s:unite_action

" ignore files
" see: http://qiita.com/naoty_k/items/0f30a226621025897390
function! s:unite_gitignore_source()
  let sources = []
  if filereadable('./.gitignore')
    for file in readfile('./.gitignore')
      if file !~ "^#\\|^\s\*$"
        call add(sources, file)
      endif
    endfor
  endif
  
  if isdirectory('./vendor')
    call add(sources, 'vendor')
  endif
  
  if isdirectory('./node_modules')
    call add(sources, 'node_modules')
  endif
  
  if isdirectory('./.git')
    call add(sources, '.git')
  endif
  
  let pattern = escape(join(sources, '|'), './|')
  call unite#custom#source('file_rec', 'ignore_pattern', pattern)
  call unite#custom#source('grep', 'ignore_pattern', pattern)
endfunction



"### unite-outline ###
nnoremap <silent> ,o :<C-u>Unite -vertical outline<CR>

if !exists("g:unite_source_outline_info")
  let g:unite_source_outline_info = {}
endif

let g:unite_source_outline_info.typescript = {
      \ 'heading': '^\s*\(module\|class\|interface\|function\|describe\|context\|it\)\>',
      \}

let g:unite_source_outline_info.stylus = {
      \ 'heading': '^\s*[#.//&]\?[//{}a-zA-Z0-9:()[\]&=_-][//{}#.a-zA-Z0-9 :()[\]&=_-]\+\s*{',
      \}



"### unite-quickfix ###
let g:unite_quickfix_filename_is_pathshorten = 0

nnoremap <silent> ,q :<C-u>Unite quickfix<CR>



"### Caw.vim ###
nmap \c <Plug>(caw:I:toggle)
vmap \c <Plug>(caw:I:toggle)

nmap \C <Plug>(caw:I:uncomment)
vmap \C <Plug>(caw:I:uncomment)



"### IndentLine ###
let g:indentLine_showFirstIndentLevel = 1



"### clang_complete ###
let g:clang_periodic_quickfix = 0
let g:clang_hl_errors = 0
let g:clang_complete_copen = 1

let g:clang_complete_auto = 0
let g:clang_auto_select = 0
let g:clang_use_library = 1

let g:clang_exec = $VIM_CLANG_PATH
let g:clang_library_path = $VIM_CLANG_LIB_PATH
let g:clang_user_options = $VIM_CLANG_OPTION



"### Wandbox ###
let g:wandbox#open_quickfix_window=0

let g:wandbox#default_compiler = {
      \ 'cpp': 'clang-head'
      \}

let g:wandbox#default_options = {
      \ 'cpp': 'gnu++1z,warning,boost-1.56'
      \}

function! s:ExecWandboxCmd(...)
  if !empty(a:000)
    let $_wandbox_arguments = join(a:000, ' ')
  endif
  
  exec "Wandbox " . $_wandbox_arguments
endfunction

let $_wandbox_arguments = ""
command! -nargs=* ExecWandbox :call s:ExecWandboxCmd(<f-args>)
nnoremap ,w :<C-u>ExecWandbox<Space>



"### vim-markdown ###
let g:vim_markdown_folding_disabled=1



"### preview ###
nnoremap ,p :PrevimOpen<CR>



"### Zenkaku Space settings ###
function! s:ZenkakuSpaceSettings()
  highlight zenkaku_space cterm=underline ctermfg=lightblue guibg=darkgray
endfunction

if has('syntax')
  augroup s:augroup_zenkaku_space
    autocmd!
    autocmd ColorScheme * call s:ZenkakuSpaceSettings()
    autocmd VimEnter,WinEnter,BufRead * let w:m1=matchadd('zenkaku_space', 'ã€€')
  augroup END
  call s:ZenkakuSpaceSettings()
endif



"### Build settings ###
function! s:BuildCmd(...)
  if !empty(a:000)
    let &makeprg = 'zsh -i -l -c "'. join(a:000, ' ') .'"'
  endif
  
  make!
endfunction

command! -nargs=* Build :call s:BuildCmd(<f-args>)

nnoremap ,b :<C-u>Build<Space>



"### Path settings ###
function! s:PathSettings()
  let l:config_file = '.vim-path'
  let l:current_dir = expand('%:p:h')
  while 1
    let l:path = l:current_dir .'/'. l:config_file
    if filereadable(l:path)
      for l:line in readfile(l:path)
        let l:line = expand(l:line)
        if l:line[0] != '/'
          let l:line = l:current_dir .'/'. l:line
        endif
        let l:line = fnamemodify(l:line, ':p')[0:-2]
        let &l:path = &g:path . l:line
      endfor
      break
    endif
    if l:current_dir == '/'
      break
    endif
    let l:current_dir = fnamemodify(l:current_dir, ':h')
  endwhile
endfunction

augroup s:augroup_path
  autocmd!
  autocmd BufReadPost * call s:PathSettings()
augroup END



"### FileType settings ###
" C++
let $_vim_cpp_stdlib_files = join(map(split($VIM_CPP_STDLIB,','), 'v:val . "/*"'), ',')

function! s:CppSettings()
  setlocal path+=$VIM_CPP_INCLUDE
  setlocal tabstop=4
  setlocal shiftwidth=4
  setlocal softtabstop=4
  setlocal matchpairs+=<:>
  
  augroup s:augroup_cpp_syntax
    autocmd!
    autocmd Syntax * call s:CppSyntaxSettings()
  augroup END
endfunction

function! s:CppSyntaxSettings()
  syntax match cppBoostPP /BOOST_PP_[A-z0-9_]*/
  highlight link cppBoostPP cppStatement
endfunction

function! s:CppStdlibSettings()
  if empty(&filetype)
    set filetype=cpp
  elseif &filetype == 'cpp'
    syntax on
  endif
endfunction

augroup s:augroup_cpp
  autocmd!
  autocmd BufReadPost $_vim_cpp_stdlib_files call s:CppStdlibSettings()
  autocmd FileType cpp call s:CppSettings()
augroup END


" TypeScript
function s:TypeScriptSettings()
  setlocal tabstop=4
  setlocal shiftwidth=4
  setlocal softtabstop=4
endfunction

augroup s:augroup_typescript
  autocmd!
  autocmd FileType typescript call s:TypeScriptSettings()
augroup END


" CoffeeScript
augroup s:augroup_coffeescript
  autocmd!
  autocmd BufReadPost *.coffee.erb setlocal filetype=coffee
augroup END


" Ruby
function s:RubySettings()
endfunction

augroup s:augroup_ruby
  autocmd!
  autocmd BufReadPost Guardfile setlocal filetype=ruby
  autocmd BufReadPost Capfile setlocal filetype=ruby
  autocmd BufReadPost *.cap setlocal filetype=ruby
  autocmd BufReadPost *.jbuilder setlocal filetype=ruby
  autocmd FileType ruby call s:RubySettings()
augroup END


" Makefile
function! s:MakefileSettings()
  setlocal noexpandtab
  setlocal tabstop=4
  setlocal shiftwidth=4
  setlocal softtabstop=4
endfunction

augroup s:augroup_makefile
  autocmd!
  autocmd FileType make call s:MakefileSettings()
augroup END


" Haskell
function! s:HaskellSettings()
  setlocal expandtab
  setlocal tabstop=2
  setlocal shiftwidth=2
  setlocal softtabstop=4
endfunction

augroup s:augroup_haskell
  autocmd!
  autocmd FileType haskell call s:HaskellSettings()
augroup END


" Markdown
function! s:MarkdownSettings()
  setlocal expandtab
  setlocal tabstop=4
  setlocal shiftwidth=4
  setlocal softtabstop=4
  IndentLinesDisable
endfunction

augroup s:augroup_markdown
  autocmd!
  autocmd BufRead,BufNewFile *.md set filetype=markdown
  autocmd FileType markdown call s:MarkdownSettings()
augroup END


" ShellScript
function! s:ShellScriptSettings()
endfunction

augroup s:augroup_shellscript
  autocmd!
  autocmd BufRead,BufNewFile .envrc* set filetype=sh
  autocmd FileType sh call s:ShellScriptSettings()
augroup END


" Java
function! s:JavaSettings()
  setlocal expandtab
  setlocal tabstop=4
  setlocal shiftwidth=4
  setlocal softtabstop=4
endfunction

augroup s:augroup_java
  autocmd!
  autocmd FileType java call s:JavaSettings()
augroup END


" Perl
function! s:PerlSettings()
  setlocal expandtab
  setlocal tabstop=4
  setlocal shiftwidth=4
  setlocal softtabstop=4
endfunction

augroup s:augroup_perl
  autocmd!
  autocmd FileType perl call s:PerlSettings()
augroup END


" PHP
function! s:PHPSettings()
  setlocal expandtab
  setlocal tabstop=4
  setlocal shiftwidth=4
  setlocal softtabstop=4
endfunction

augroup s:augroup_php
  autocmd!
  autocmd FileType php call s:PHPSettings()
augroup END
